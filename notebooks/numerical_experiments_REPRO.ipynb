{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Numerical Experiments\n",
    "\n",
    "With this notebook, you can experiment with some Runge-Kutta methods as described in the paper.\n",
    "\n",
    "Run the cell below to activate the project environment containing the necessary packages."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import Pkg\n",
    "Pkg.activate(\".\")\n",
    "Pkg.instantiate()"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Setup\n",
    "\n",
    "Run this cell once to setup some basic functions."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using StaticArrays\n",
    "using OrdinaryDiffEq, DiffEqCallbacks\n",
    "using SummationByPartsOperators\n",
    "using LinearAlgebra, Printf\n",
    "using Quadmath\n",
    "\n",
    "using LaTeXStrings\n",
    "import PyPlot; plt = PyPlot\n",
    "using PyCall\n",
    "\n",
    "# line cycler adapted to colourblind people\n",
    "cycler = pyimport(\"cycler\")\n",
    "cycler.cycler\n",
    "colours = [\"#E69F00\", \"#56B4E9\", \"#009E73\", \"#0072B2\", \"#D55E00\", \"#CC79A7\", \"#F0E442\"]\n",
    "linestyles = [\"-\", \"--\", \"-.\", \":\", \"-\", \"--\", \"-.\"]\n",
    "#markers = [\">\", \"^\", \"<\", \"v\", \"+\", \"x\", \".\"]\n",
    "markers = [\"4\", \"2\", \"3\", \"1\", \"+\", \"x\", \".\"]\n",
    "colourblind_cycler = (cycler.cycler(color=colours) + cycler.cycler(linestyle=linestyles))\n",
    "plt.rc(\"axes\", prop_cycle=colourblind_cycler)\n",
    "plt.rc(\"lines\", linewidth=2, markersize=12, markeredgewidth=1.5)\n",
    "\n",
    "plt.rc(\"text\", usetex=true)\n",
    "plt.rc(\"text.latex\", preamble=\"\\\\usepackage{newpxtext}\\\\usepackage{newpxmath}\\\\usepackage{commath}\\\\usepackage{mathtools}\")\n",
    "plt.rc(\"font\", family=\"serif\", size=18.)\n",
    "plt.rc(\"savefig\", dpi=200)\n",
    "plt.rc(\"legend\", fontsize=\"medium\", fancybox=true, framealpha=0.5)\n",
    "\n",
    "isdir(\"../figures\") || mkdir(\"../figures\")\n",
    "\n",
    "\n",
    "# the explicit midpoint method\n",
    "A = [\n",
    "    0 0;\n",
    "    1//2 0\n",
    "]\n",
    "b = [0, 1//1]\n",
    "c = A * (zero(b) .+ 1)\n",
    "order = 2\n",
    "midpoint_rk2 = ExplicitRK(tableau=DiffEqBase.ExplicitRKTableau(A, c, b, order))\n",
    "\n",
    "# a strongly stable second order method\n",
    "A = [\n",
    "    0//1 0 0 0; \n",
    "    1 0 0 0; \n",
    "    1 -1 0 0; \n",
    "    -1 1 1 0\n",
    "]\n",
    "b = [1//4, 1//4, 1//4, 1//4]\n",
    "c = A * (zero(b) .+ 1)\n",
    "order = 2\n",
    "stable_rk2 = ExplicitRK(tableau=DiffEqBase.ExplicitRKTableau(A, c, b, order))\n",
    "\n",
    "# a strongly stable third order method\n",
    "A = [\n",
    "    0 0 0 0 0; \n",
    "    1//2 0 0 0 0;\n",
    "    1 0 0 0 0; \n",
    "    1 0 -1 0 0; \n",
    "    -3 2 1 1 0\n",
    "]\n",
    "b = [0, 2//3, 0, 1//6, 1//6]\n",
    "c = A * (zero(b) .+ 1)\n",
    "order = 3\n",
    "stable_rk3 = ExplicitRK(tableau=DiffEqBase.ExplicitRKTableau(A, c, b, order))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Time-Dependent Linear Operators\n",
    "\n",
    "Here, the linear wave equation\n",
    "\n",
    "\\begin{equation}\n",
    "\\begin{aligned}\n",
    "  \\partial_t u(t,x) + \\sin(t^2) \\partial_x u(t,x) &= 0,\n",
    "  && t \\in (0,100), x \\in [-1, 1],\n",
    "  \\\\\n",
    "  u(0,x) &= \\sin(\\pi x),\n",
    "  && x \\in [-1,1],\n",
    "\\end{aligned}\n",
    "\\end{equation}\n",
    "\n",
    "with time-dependent coefficients and periodic boundary conditions is solved numerically."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function compute_advection_sint2(alg, dt, u0func)\n",
    "    T = typeof(dt)\n",
    "\n",
    "    D = periodic_derivative_operator(1, 2, -one(T), one(T), 50)\n",
    "    x = SummationByPartsOperators.grid(D)\n",
    "    u0 = u0func.(x)\n",
    "    f = (du,u,D,t) -> mul!(du, D, u, -sin(t^2))\n",
    "    tspan = (zero(T), 100*one(T))\n",
    "    ode = ODEProblem(f, u0, tspan, D)\n",
    "\n",
    "    saveat = range(first(tspan), stop=last(tspan), length=1000)\n",
    "    saving = SavingCallback((u,t,int)->integrate(u->u^2, u, D), SavedValues(T,T), saveat=saveat)\n",
    "    sol = solve(ode, alg, dt=dt, callback=saving, save_everystep=false, adaptive=false)\n",
    "    t = saving.affect!.saved_values.t\n",
    "    energy = saving.affect!.saved_values.saveval\n",
    "\n",
    "    t, energy, opnorm(Matrix(D))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "plt.close(\"all\")\n",
    "\n",
    "t, energy, opnormD = compute_advection_sint2(SSPRK33(), 1.e-5, sinpi)\n",
    "@printf(\"The operator norm of D with respect to the standard inner product is %.3e.\\n\", opnormD)\n",
    "plt.plot(t, energy, label=\"SSPRK33\")\n",
    "\n",
    "t, energy, opnormD = compute_advection_sint2(stable_rk2, 1.e-5, sinpi)\n",
    "plt.plot(t, energy, label=\"Stable RK42\")\n",
    "\n",
    "plt.xlabel(L\"t\")\n",
    "plt.ylabel(L\"|\\!| u(t) |\\!|^2\")\n",
    "plt.legend()\n",
    "plt.xlim(extrema(t))\n",
    "plt.tight_layout()\n",
    "\n",
    "plt.savefig(\"../figures/linear_time_dep_advection.pdf\", bbox_inches=\"tight\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# A Special Nonlinear Autonomous System\n",
    "\n",
    "The explicit midpoint method\n",
    "\n",
    "\\begin{align}\n",
    "    y_2 & = u^n + \\frac{h}{2}f(u^n), \\\\\n",
    "    u^{n+1} & = u^n + h f(y_2)\n",
    "\\end{align}\n",
    "\n",
    "conserves the energy for the ODE\n",
    "\n",
    "\\begin{align}\n",
    "u_1'(t) & = \\frac{-u_2}{u_1^2 + u_2^2}, \\\\\n",
    "u_2'(t) & = \\frac{ u_1}{u_1^2 + u_2^2}.\n",
    "\\end{align}\n",
    "\n",
    "and all step sizes."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function compute_special_problem(alg, dt)\n",
    "  T = typeof(dt)\n",
    "\n",
    "  u0 = SVector(one(T), zero(T))\n",
    "  f = (u,p,t) -> SVector(-u[2], u[1]) / sum(abs2, u)\n",
    "  tspan = (zero(T), 20*one(T))\n",
    "  ode = ODEProblem(f, u0, tspan)\n",
    "  saving = SavingCallback((u,t,int)->sum(abs2, u), SavedValues(T,T))\n",
    "  sol = solve(ode, alg, dt=dt, callback=saving, save_everystep=false, adaptive=false)\n",
    "\n",
    "  t = saving.affect!.saved_values.t\n",
    "  energy = saving.affect!.saved_values.saveval\n",
    "\n",
    "  t, energy\n",
    "end\n",
    "\n",
    "\n",
    "function create_plots(dt; yaxis_log=false, save_legend=false)\n",
    "    algs = [SSPRK33(), midpoint_rk2, stable_rk2, stable_rk3]\n",
    "    labels = [\"SSPRK33\", \"Midpoint\", \"Stable RK42\", \"Stable RK53\"]\n",
    "\n",
    "    plt.close(\"all\")\n",
    "\n",
    "    for (alg, label) in zip(algs, labels)\n",
    "        t, energy = compute_special_problem(alg, dt)\n",
    "\n",
    "        plt.plot(t, energy .- first(energy), label=label)\n",
    "    end\n",
    "\n",
    "    plt.xlabel(L\"t\")\n",
    "    plt.ylabel(L\"|\\!| u(t) |\\!|^2 - |\\!| u_0 |\\!|^2\")\n",
    "    plt.xlim((0, 20))\n",
    "    plt.gca().ticklabel_format(axis=\"y\", scilimits=(0,0))\n",
    "    plt.tight_layout()\n",
    "    yaxis_log && plt.yscale(\"symlog\", linthreshy=1.e-10)\n",
    "\n",
    "    plt.savefig(\"../figures/special_problem__dt_$(-round(Int, Float64(log10(dt)))).pdf\", bbox_inches=\"tight\")\n",
    "\n",
    "    if save_legend\n",
    "        ax = plt.gca()\n",
    "        plt.figure()\n",
    "        handles, labels = ax.get_legend_handles_labels()\n",
    "        plt.figlegend(handles, labels, loc=\"center\", ncol=4)\n",
    "        plt.savefig(\"../figures/special_problem__legend.pdf\", bbox_inches=\"tight\")\n",
    "    end\n",
    "    \n",
    "    nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "T = Float128\n",
    "create_plots(T(1.e-1), yaxis_log=false, save_legend=true)\n",
    "create_plots(T(1.e-5), yaxis_log=false, save_legend=false)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.2.0",
   "language": "julia",
   "name": "julia-1.2"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.2.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 1
}
